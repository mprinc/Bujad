globals[
  global_calculation_1
  global_calculation_2
  global_calculation_3
]

to finalize-report
  ask activity-types[die]
  ask activities[die]
  ask activity-type-activities[die]
  ask activity-actors[die]
  ask iamwhats[die]
  ask iamwhat-actors[die]
  ask iamwhat-activities[die]
end


; ========================================================================================
; measures
; ========================================================================================

to measure-activities-per-node
  ask actors[
    let activities-num activities-number-for-actor self
    set label word "actor-" word who word ":" activities-num
    set calculation_actor_1 activities-num
  ]  
end

to measure-happiness-per-node
  set global_calculation_1 happinessForSystem
end

; calcullates happiness for the whole system
; it calculates hapiness as a sum of happiness of each node
; it is normalized to the [0,1] interval
to-report happinessForSystem
; callculate and show activities num for each actor
  let globalHappiness 0
  ask actors[
    let actorHappiness happinessForActor self

    set label word "actor-" word who word ":" actorHappiness
    set calculation_actor_1 actorHappiness

    set globalHappiness globalHappiness + actorHappiness
    ;show word "globalHappiness = " word globalHappiness word ", actorHappiness = " actorHappiness
  ]
  set globalHappiness globalHappiness / (count actors)
  show word "globalHappiness = " globalHappiness
  report globalHappiness
end

; callculate happiness for actor
; it calulates happiness as a sum of happiness of all friendships of a specified actor
; note, it doesn't calculate happiness toward ALL actors in the system but only friends
; however, happiness depends on balance of activities exchanged and any two nodes that exchanged activities have to be friends (at least because they become friends through that eaction exchange)
; it is normalized to the [0,1] interval
to-report happinessForActor [actorSelected]
  let actorHappiness 0
  ask actorSelected[
    ; link-neighbors
    ; find all actors that are friends of self actor
    let actorFriendships sort friendship-neighbors
    ;show word "actor: " word self word ", actorFriendships: " actorFriendships
    foreach actorFriendships[
      let friendshipHappiness happinessForFriendship self ?
      set actorHappiness actorHappiness + friendshipHappiness
      ;show word "actorHappiness = " word actorHappiness word ", friendshipHappiness = " friendshipHappiness
    ]
    if(length actorFriendships > 0)[
      set actorHappiness actorHappiness / (length actorFriendships)
    ]
  ]
  report actorHappiness
end

; calculates happiness for speciffic friendship betwwn actorFrom and actorTo
; it is normalized to the [0,1] interval
to-report happinessForFriendship [actorFrom actorTo]
  let friendshipHappiness 0
  let activitiesIn activities-in-number-for-friendship actorFrom actorTo
  let activitiesOut activities-out-number-for-friendship actorFrom actorTo
  if(activitiesIn != 0 or activitiesOut != 0)[
    set friendshipHappiness 1 - (abs(activitiesIn - activitiesOut) / (activitiesIn + activitiesOut))
  ]
  ;show word "friendshipHappiness = " word friendshipHappiness word ", activitiesIn = " word activitiesIn word ", activitiesOut " activitiesOut
  report friendshipHappiness
end

to show-actor-activites-num
  show "show-actor-activites-num"
  ask actors[
    let activities-num activities-number-for-actor self
    set label word "actor-" word who word ":" activities-num
  ]  
end


; per friendship commands
; ========================================================================================

to-report activities-in-number-for-friendship [actorMaster actorSlave]
  let activitiesNum 0
  ask actorMaster[
    ; all links for actorMaster
    ; my-in-links ; Reports an agentset of all the directed links coming in from other nodes to the caller.
    let activityLinksIn sort my-in-activity-actors with [happened = true]
    foreach activityLinksIn[
      let activityIn [end1] of ?
      if(activityIn = actorMaster)[set activityIn [end2] of ?]
      ask activityIn[
        ; in-link-from
        let activityLinkIn in-activity-actor-from actorSlave
        if((activityLinkIn != nobody) and ([happened] of activityLinkIn) = true) [set activitiesNum activitiesNum + 1]
      ]
    ]
  ]
  report activitiesNum
end

to-report activities-out-number-for-friendship [actorMaster actorSlave]
  let activitiesNum 0
  ask actorMaster[
    ; all links for actorMaster
    ;my-in-links ; Reports an agentset of all the directed links coming in from other nodes to the caller.
    let activityLinksOut sort my-out-activity-actors with [happened = true]
    foreach activityLinksOut[
      let activityOut [end1] of ?
      if(activityOut = actorMaster)[set activityOut [end2] of ?]
      ask activityOut[
        ; in-link-from
        let activityLinkOut out-activity-actor-to actorSlave
        if((activityLinkOut != nobody) and ([happened] of activityLinkOut) = true) [set activitiesNum activitiesNum + 1]
      ]
    ]
  ]
  report activitiesNum
end
; per actor commands
; ========================================================================================

to-report activities-number-for-actor [actorSelected]
  report (activities-in-number-for-actor actorSelected) + (activities-out-number-for-actor actorSelected)
end

to-report activities-in-number-for-actor [actorSelected]
  let activitiesNum 0
  ask actorSelected[
    ; equivalent to command in-<breed>-neighbors
    set activitiesNum count my-in-activity-actors with [happened = true]
  ]
  report activitiesNum
end

to-report activities-out-number-for-actor [actorSelected]
  let activitiesNum 0
  ask actorSelected[
    ; equivalent to command out-<breed>-neighbors
    set activitiesNum count my-out-activity-actors with [happened = true]
  ]
  report activitiesNum
end