; In order to rise error (to make it possible for this file to be loaded inside of NetLogo environment) just keep this line uncommented. There are too many error to NetLogo not notice them ;) :x. Comment it while you are running code. You need it only on loading model in NetLogo

globals[
    activities-num-emitted   ; total number of activities emitted in the system from all actors cumullatively
]

; we iterate through activities and remove obsolated ones together with their links
to remove-obsolated-activities
  if(length activities-list > 0)[
    let activitySelected first activities-list
    let lastValidTime ticks - Activity-forgetting-time
    
    while [[time] of activitySelected < lastValidTime][
      set activities-list but-first activities-list
      ask activitySelected[
        ; kill all links
        
        ; equivalent to command my-out-<breeds>
        ask my-out-activity-actors [die]
        ; equivalent to command my-in-<breeds>
        ask my-in-activity-actors [die]
        ask my-iamwhat-activities [die]
        ask my-activity-type-activities [die] 
      ]
      ask activitySelected[die]
      set activitySelected first activities-list
    ]
  ]
end

to-report diffuse-activity
  ; ask actor (actor-difuser) that is not in emittion saturation to emit an activity
  ; it will decide if it will emit it in according to emission probability
  let actor-difuser-potential one-of actors with [price-emitted < price-emission-limit]
  report diffuse-activity-for-actor actor-difuser-potential
end

; command diffuses activity for specified actor: actor-difuser-potential
; external testing: let a diffuse-activity-for-actor actor 18
; it reports activity (or nobody) that is diffused (emitted)
to-report diffuse-activity-for-actor [actor-difuser-potential]
  ; type "\n"
  ; type "\n"
  ;show word "[diffuse-activity-for-actor] actor-difuser-potential: " actor-difuser-potential
  let activity-current nobody
  let actor-difuser nobody
  
  if(actor-difuser-potential = nobody)[
    report nobody
  ]

  ; 
  if([price-emitted] of actor-difuser-potential >= [price-emission-limit] of actor-difuser-potential)[
    show word "[diffuse-activity-for-actor] Error! Selected actor has price-emitted >= than price-emission-limit. Actor: " actor-difuser-potential
    report nobody
  ]
  ;; create activity breed that will represent the activity that just happened
  create-activities 1
  [
    ; hide links between 
    if(Last-activity-links and length activities-list > 1)[
      let activityLast item (length activities-list - 2) activities-list  
      ask activityLast[
        ; equivalent to command my-out-<breeds>
        ask my-out-activity-actors [
          if(Links-show-extra = false)[set hidden? true]
        ]
        ; equivalent to command my-in-<breeds>
        ask my-in-activity-actors [
        if(Links-show-extra = false)[set hidden? true]
        ]
      ]
    ]
      
    set activities-list lput self activities-list
    set activity-current self
    
    setxy (max-pxcor - 5) 0 ; we need to update it AFTER we se if activity DID happen in the ind
    set size 5
    ;set hidden? true
    set shape "flower"
    set time ticks
  ]
  
  if(actor-difuser-potential != nobody)[
    ask actor-difuser-potential [
      ifelse (random-float 1 <= Activity-emission-probability)[
        set actor-difuser self
        ; ask actor actor-difuser to diffuse the activity activity-current
        ; ----------------------------------------------------------------
        actor-diffuse-activity-from activity-current
      ][
        ;show word "actor : " word self " rejected to diffuse activity"
      ]
    ]
  ]
  
  ; if actor 'refused' to difuse activity we have to revoke activity
  ifelse (actor-difuser = nobody)[
    ask activity-current [die]
    set activities-list but-last activities-list
  ][
    ; we move activities along the activity line and hide those that left the visible area
    ; ------------------------------------------------------------------------------------------------

    ; we add 1 since we will put overflowed activities on the first position
    let activity-y (max-pycor - 15 * (1 + length activities-list))
  
    ; type "activity-y = " type activity-y type ", min-pycor = " type min-pycor type "\n"
    if(activity-y < min-pycor + 15)[
      ask activities with [ycor < (max-pycor - 8)][
        set ycor ycor + 8;
      ]
      set activity-y min-pycor + 15
    ]

    ; all overflowed activites should be hidden together with their activity-actors links
    ask activities with [ycor >= (max-pycor - 5)][
      set hidden? true;

      ; equivalent to command my-out-<breeds>
      ask my-out-activity-actors [
        set hidden? true;
      ]
      ; equivalent to command my-in-<breeds>
      ask my-in-activity-actors [
        set hidden? true;
      ]
    ]

    ask activity-current[
      set ycor activity-y
    ]
  ]

  report activity-current
end

; the chosen actor-difuser will send activity activity-current to one (soon more) other actors that didn't reach absorption limit
to actor-diffuse-activity-from [activity-current]
  ; type "actor : " type self type "is diffusing activity\n"

  actor-populate-activity activity-current
  let activity-type-current nobody
  ; no need for one-of (there has to be only one link) except for de-agentseting to breed
  ask activity-current[set activity-type-current one-of activity-type-activity-neighbors]

  ; consider emitting price
  set price-emitted price-emitted + [price-to-diffuse] of activity-type-current
  
  ; create a link between actor emitter and activity
  create-activity-actor-to activity-current [
    set shape "default"
    set color yellow
    set happened true
    if(not Last-activity-links and Links-show-extra = false)[set hidden? true]
  ]
  
  ; find (if any) actor that emitter is emitting activity toward
  let actor-absorbers actor-sender-chooses-receiver-for-emitting-activity activity-current
  if(actor-absorbers != nobody)[

    ; ask absorber if he is willing to absorb
    let actorReceiverAccepted actor-receiver-decides-on-absorption-of-activity self actor-absorbers activity-current

    ; connect activity with absorber
    ask actor-absorbers[

      ; myself: emitter
      ; self: absorber
      ;show word " let friendshipToAbsorber... " self
      let friendshipToAbsorber friendship ([who] of myself) ([who] of self)
      let timeToLostValue get-turtle-link-endpoint-value myself friendshipToAbsorber "time-to-lost"

      ;show word "[actor-diffuse-activity-from] actorEmitter: " word myself word ", actorAbsorber: " word self word ", friendshipToAbsorber: " word friendshipToAbsorber word ", time-to-lost: " timeToLostValue

      if(timeToLostValue <= Friendship-Triger-On-Time)[
        ;show word "Recovered friendship: " word myself word " -> " self
      ]

      ; TODO: this code should be probable corrected
      ; It seems it sets the life of friendship (time-to-lost) equal to manual-attractiveness
      ; which later should be set separatelly
      ; I guess it is set as it is just to simplify simulation and chance to have time-to-lost non uniformly set
      let manualAttractiveness get-turtle-link-endpoint-value myself friendshipToAbsorber "manual-attractiveness"
      set-turtle-link-endpoint-value myself friendshipToAbsorber "time-to-lost" manualAttractiveness

      ; create links between actors and activity
      create-activity-actor-from activity-current [
        set shape "default"
        
        ifelse(actorReceiverAccepted)[
          set color red
          set happened true
        ][
          set color blue
          set happened false
        ]

        if(not Last-activity-links and Links-show-extra = false)[set hidden? true]
      ]
    ]
    
    if(actorReceiverAccepted)[
      ask actor-absorbers [
        ; consider absorbtion price
        set price-absorbed price-absorbed + [price-to-absorb] of activity-type-current
        let friendship-absorber friendship-with myself
        ;show word "[actor-diffuse-activity-from] \"link:activity:sent\" for friendship: " friendship-absorber
        visualization-set-state-friendship friendship-absorber "link:activity:sent"
      ]
      ; affect-relatinships-by-activity activity-current ; DEPRICATED-OLD-MODEL
    ]
  ]
end

; in the context of actor-sender
; it returns the most appropriate actor
to-report actor-sender-chooses-receiver-for-emitting-activity [activity-current]
  let actorReceiver nobody
  
  ; check if we are in the Friendship Manage mode and
  ; check if there are some dying friendship
  if(FM?)[
    if(FM-type = "TimeToLoose")[
      ;show "[actor-sender-chooses-receiver-for-emitting-activity] Friendship-Manager scanning"
      let minTimeToLostValue 1000000000000000;
      let  minTimeToLostLink nobody;
      let  minTimeToLostActor nobody;
      let timeToLostValue 1000000000000000;
      
      foreach sort friendship-neighbors[
        ;show word "? (actor): " ?
        let friendshipSelected friendship-with ?
        set timeToLostValue actor-get-turtle-link-endpoint-value friendshipSelected "time-to-lost"
        ;show word "neighbour actor: " word ? word ", friendshipSelected: " word friendshipSelected word ", time-to-lost: " timeToLostValue
        if(timeToLostValue < minTimeToLostValue)[
          set minTimeToLostValue timeToLostValue
          set minTimeToLostLink friendshipSelected
          set minTimeToLostActor ?
        ]
      ]
      ; do not react if the minimum lost time over all nodes is higher than time on what we should triger reaction
      if(minTimeToLostValue <= Friendship-Triger-On-Time)[
        set actorReceiver minTimeToLostActor
        ;show word "[actor-sender-chooses-receiver-for-emitting-activity] Friendship-Manager scanning: Critical actor: " actorReceiver
      ]
    ]
    if(FM-type = "Happiness(FlowIn)")[
      ;activities-in-number-for-friendship [actorMaster actorSlave]
      ;activities-out-number-for-friendship [actorMaster actorSlave]
    ]
  ]

  if(actorReceiver = nobody and Chooser-whom-to-send = "anyone")[ ; IMPLEMENTED!
    set actorReceiver one-of other actors with [price-absorbed < price-absorption-limit];
  ]
  if(actorReceiver = nobody and Chooser-whom-to-send = "friends")[ ; IMPLEMENTED!
    set actorReceiver one-of friendship-neighbors with [price-absorbed < price-absorption-limit];
  ]
  if(actorReceiver = nobody and Chooser-whom-to-send = "popularity")[ ; IMPLEMENTED!
    ;let actorsSorted sort actors
    let actorsSorted sort friendship-neighbors
    let popularitySum 0
    foreach actorsSorted[
      set popularitySum popularitySum + [popularity] of ?
    ]

    let probabilityAdopted random-float popularitySum
    ;show word "popularitySum : " word popularitySum word ", probabilityAdopted = " probabilityAdopted

    set popularitySum 0
    foreach actorsSorted[
      set popularitySum popularitySum + [popularity] of ?
      if(actorReceiver = nobody and popularitySum > probabilityAdopted)[
        set actorReceiver ?
        ;show word "actorReceiver : " actorReceiver
      ]
    ]
  ]
  if(actorReceiver = nobody and Chooser-whom-to-send = "attractiveness_manual")[ ; IMPLEMENTED!
    let actorsSorted sort friendship-neighbors
    let attractivenessManualSum 0
    foreach actorsSorted[
      let friendshipToOther friendship-with ?
      let value actor-get-turtle-link-endpoint-value friendshipToOther "manual-attractiveness"
      set attractivenessManualSum attractivenessManualSum + value 
    ]
    
    let probabilityAdopted random-float attractivenessManualSum
    ;show word "attractivenessManualSum : " word attractivenessManualSum word ", probabilityAdopted = " probabilityAdopted

    set attractivenessManualSum 0
    foreach actorsSorted[
      let friendshipToOther friendship-with ?
      let value actor-get-turtle-link-endpoint-value friendshipToOther "manual-attractiveness"
      set attractivenessManualSum attractivenessManualSum + value 

      if(actorReceiver = nobody and attractivenessManualSum > probabilityAdopted)[
        set actorReceiver ?
        ;show word "actorReceiver : " actorReceiver
      ]
    ]
  ]
  if(actorReceiver = nobody and Chooser-whom-to-send = "shared_interest")[ ; IMPLEMENTED!
    let actorsSorted sort friendship-neighbors
    let sharedInterestSum 0
    foreach actorsSorted[
      set sharedInterestSum sharedInterestSum + count get-shared-iamwhats self ?
    ]

    let probabilityAdopted random-float sharedInterestSum
    ;show word "sharedInterestSum : " word sharedInterestSum word ", probabilityAdopted = " probabilityAdopted

    set sharedInterestSum 0
    foreach actorsSorted[
      set sharedInterestSum sharedInterestSum + count get-shared-iamwhats self ?
      if(actorReceiver = nobody and sharedInterestSum > probabilityAdopted)[
        set actorReceiver ?
        ;show word "actorReceiver : " actorReceiver
      ]
    ]
  ]
  if(actorReceiver = nobody and Chooser-whom-to-send = "environmental")[
  ]
  ;show word "[actor-sender-chooses-receiver-for-emitting-activity] actorReceiver: " actorReceiver
  report actorReceiver
end

; absorber answers if (s)he is willing to absorb
to-report actor-receiver-decides-on-absorption-of-activity [actorSender actorReceiver activity-current]
  let shouldAccept false
  
  if([price-absorbed] of actorReceiver >= [price-absorption-limit] of actorReceiver) [
    ;show word "[actor-receiver-decides-on-absorption-of-activity] Rejected! ActorReceiver has price-absorbed (" word ([price-absorbed] of actorReceiver) word ") >= than price-absorption-limit (" word ([price-absorption-limit] of actorReceiver) word "). actorReceiver: " actorReceiver
    report false
  ]

  if(random-float 1 > Activity-absorption-probability) [
    ;show word "[actor-receiver-decides-on-absorption-of-activity] Rejected! ActorReceiver decision was outside of Activity-absorption-probability.  actorReceiver: " actorReceiver
    report false
  ]

  if(Chooser-whom-to-receive = "anyone")[ ; IMPLEMENTED! 
    set shouldAccept true
  ]
  if(Chooser-whom-to-receive = "friends")[ ; IMPLEMENTED!
    ask actorReceiver[
      set shouldAccept member? actorSender friendship-neighbors
    ]
  ]
  if(Chooser-whom-to-receive = "popularity")[
  ]
  if(Chooser-whom-to-receive = "attractiveness_manual")[
  ]
  if(Chooser-whom-to-receive = "shared_interest")[
  ]
  if(Chooser-whom-to-receive = "environmental")[
  ]
  ;show word "[actor-receiver-decides-on-absorption-of-activity] ActorReceiver (" word actorReceiver word ") decission on acceptance: " shouldAccept
  report shouldAccept
end

; it populates activity
to actor-populate-activity [activity-current]
  set activities-num-emitted activities-num-emitted + 1
  ; select one iamwhat from actor's iamwhats
  let selected-iamwhat one-of iamwhat-actor-neighbors
  ; select random activity-type
  let selected-activity-type one-of activity-types;
  
  ask activity-current[
    set intensity ([intensity] of selected-activity-type)
    set shape ([shape] of selected-activity-type)

    ; create link between activity and iamwhat
    create-iamwhat-activity-with selected-iamwhat [
      set shape "default"
      set color brown
      if(Links-show-extra = false)[set hidden? true]
    ]

    ; create link between activity-type and activity
    create-activity-type-activity-with selected-activity-type [
      set shape "default"
      set color brown
      if(Links-show-extra = false)[set hidden? true]
    ]
  ]
end

to fm-tick-update
  ask actors[
    foreach sort my-friendships[
      actor-set-turtle-link-endpoint-value ? "time-to-lost" (-1 + actor-get-turtle-link-endpoint-value ? "time-to-lost")
    ]
  ]
end